def versionMajor = 0 // incompatible API changes
def versionMinor = 4 // added functionality in a backwards-compatible manner
def versionPatch = 0 // backwards-compatible bug fixes
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "net.freifunk.paderborn.krombel"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "versionCode", "${versionMajor}.${versionMinor}.${versionPatch}"
        }
        debug {
            resValue "string", "versionCode", "${versionMajor}.${versionMinor}.${versionPatch}"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }

    if (project.hasProperty("krombel.signing")) {
        println "project has property krombel.signing:"
        println project.property("krombel.signing")
        if (new File(project.property("krombel.signing") + ".gradle").exists()) {
            apply from: project.property("krombel.signing") + ".gradle";
            println "Applied properties"
        }
    }
}


dependencies {
    // androidannotations deps need to be here - otherwise code gen does not work
    apt "org.androidannotations:androidannotations:3.2"
    compile 'org.androidannotations:androidannotations:3.2'
    compile project(':core')
}
